
Name Quality

case(
  when(
    (
      ${data_quality.name_quality} = "Client doesn't know" OR
      ${data_quality.name_quality} = "Client prefers not to answer"
    ),
    "Client Doesn't Know OR Preferred NOT to Answer"
  ),
  when(
    (
      ${data_quality.name_quality} = "Data not collected" OR
      (
        is_null(${data_quality.first_name}) AND
        is_null(${data_quality.last_name})
      )
    ),
    "Missing Data"
  ),
  when(
    (
      ${data_quality.name_quality} = "Partial, street name, or code name reported" OR
      is_null(${data_quality.first_name}) OR
        is_null(${data_quality.last_name})
    ),
    "Data Issues"
  ),
  "No Issues"
)



==================================
SSN QUALITY


case(
  when(
    ${data_quality.ssn_quality} = "Approximate or partial SSN reported" OR
    length(${data_quality.ssn}) != 9 OR
    substring(${data_quality.ssn}, 6, 4) = "0000" OR
    substring(${data_quality.ssn}, 1, 3) = "000" OR
    substring(${data_quality.ssn}, 1, 3) = "666" OR
    substring(${data_quality.ssn}, 1, 1) = "9" OR
    substring(${data_quality.ssn}, 4, 2) = "00" OR
    ${data_quality.ssn} = "123456789" OR
    ${data_quality.ssn} = "987654321" OR
    (
      substring(${data_quality.ssn}, 2, 1) = substring(${data_quality.ssn}, 1, 1) AND
      substring(${data_quality.ssn}, 3, 1) = substring(${data_quality.ssn}, 1, 1) AND
      substring(${data_quality.ssn}, 4, 1) = substring(${data_quality.ssn}, 1, 1) AND
      substring(${data_quality.ssn}, 5, 1) = substring(${data_quality.ssn}, 1, 1) AND
      substring(${data_quality.ssn}, 6, 1) = substring(${data_quality.ssn}, 1, 1) AND
      substring(${data_quality.ssn}, 7, 1) = substring(${data_quality.ssn}, 1, 1) AND
      substring(${data_quality.ssn}, 8, 1) = substring(${data_quality.ssn}, 1, 1) AND
      substring(${data_quality.ssn}, 9, 1) = substring(${data_quality.ssn}, 1, 1)
    )
    ,
    "Data Issues"
  ),
  "No Issues"
)
